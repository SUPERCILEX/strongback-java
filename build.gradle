buildscript {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'http://first.wpi.edu/FRC/roborio/maven/release' }
        maven { url 'http://first.wpi.edu/FRC/roborio/maven/development' }
        maven { url 'http://dev.imjac.in/maven' }
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'http://first.wpi.edu/FRC/roborio/maven/release' }
        maven { url 'http://first.wpi.edu/FRC/roborio/maven/development' }
        maven { url 'http://dev.imjac.in/maven' }
    }

    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = 1.8

    // No-op if it's the root lib
    if (project.name == "strongback-java" || project.name == "build") return

    // Task to generate sources JAR
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.java.srcDirs
    }

    // Use tasks above to define artifacts
    artifacts {
        archives sourcesJar
    }

    // So that we can define publication
    project.apply plugin: 'maven-publish'

    publishing {
        // By passing -Pcustom_local=/some/path and running the
        // publishLibraryPublicationToCustomLocalRepository task you can publish this library to a
        // custom maven repository location on your machine.
        repositories {
            maven {
                name 'CustomLocal'
                url uri(project.hasProperty('custom_local') ? project.getProperty('custom_local') : '/tmp/')
            }
        }

        repositories {
            maven {
                name 'BuildLocal'
                url "$buildDir/repo"
            }
        }
    }

    publishing {
        publications {
            "${project.name}"(MavenPublication) {

                groupId "org.strongback"
                artifactId project.name
                version "1.0.0"

                artifact "$buildDir/libs/${project.name}.jar"
                artifact sourcesJar

                pom.withXml {
                    // Dependencies
                    def dependenciesNode = asNode().getAt("dependencies")[0]
                    if (dependenciesNode == null) {
                        dependenciesNode = asNode().appendNode("dependencies")
                    }

                    // Add all that are 'compile'
                    configurations.compile.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
